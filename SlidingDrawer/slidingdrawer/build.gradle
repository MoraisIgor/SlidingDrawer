plugins {

    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.dcendents.android-maven' version '2.0'
}

apply plugin: 'com.android.library'

group = 'com.github.moraisigor'
version = '1.7.0'

android {

    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {

        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName '1.0.0'
    }

    buildTypes {

        release {

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {

    // Support
    implementation "com.android.support:support-annotations:${rootProject.ext.supportVersion}"
}

task javadoc(type: Javadoc) {

    source = android.sourceSets.main.java.srcDirs

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {

    classifier = 'javadoc'

    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {

    from android.sourceSets.main.java.srcDirs

    classifier = 'sources'
}

artifacts {

    archives javadocJar
    archives sourcesJar
}

install {

    repositories.mavenInstaller {

        pom {

            project {

                packaging 'aar'

                name 'SlidingDrawer'
                description 'SlidingDrawer hides content out of the screen and allows the user to drag a handle to bring the content on screen.'
                url 'https://github.com/MoraisIgor/SlidingDrawer'

                licenses {

                    license {

                        name 'Apache Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {

                    developer {

                        id 'MoraisIgor'
                        name 'Igor Morais'
                        email 'igor.morais@hollowsoft.com'
                    }
                }

                scm {

                    url 'https://github.com/MoraisIgor/SlidingDrawer'
                    connection 'https://github.com/MoraisIgor/SlidingDrawer.git'
                    developerConnection 'https://github.com/MoraisIgor/SlidingDrawer.git'
                }
            }
        }
    }
}

bintray {

    Properties p = new Properties()

    p.load(project.rootProject.file('local.properties').newDataInputStream())

    user = p.getProperty('bintray.user')
    key = p.getProperty('bintray.key')

    publish = true

    configurations = ['archives']

    pkg {

        repo = 'Maven'

        name = 'SlidingDrawer'
        desc = 'SlidingDrawer hides content out of the screen and allows the user to drag a handle to bring the content on screen.'
        labels = ['android', 'java', 'widget']
        licenses = ['Apache-2.0']

        websiteUrl = 'https://github.com/MoraisIgor/SlidingDrawer'
        issueTrackerUrl = 'https://github.com/MoraisIgor/SlidingDrawer/issues'

        vcsUrl = 'https://github.com/MoraisIgor/SlidingDrawer.git'

        version {

            gpg {

                sign = true
                passphrase = p.getProperty('bintray.password')
            }
        }
    }
}
